buildscript {
  repositories {
    jcenter()
  }
}

plugins {
  id 'java'
  id 'groovy'
  id 'java-gradle-plugin'
  id 'maven-publish'
  id "com.jfrog.bintray" version "1.8.4"
}

repositories {
  jcenter()
}

group 'com.github.etinaa'
version '0.1.0'

dependencies {
  compile localGroovy()
  compile 'net.masterthought:cucumber-reporting:4.1.0'

  testCompile gradleTestKit()
  testCompile('org.spockframework:spock-core:1.2-groovy-2.4') {
    exclude module: 'groovy-all'
  }
}

gradlePlugin {
  plugins {
    simplePlugin {
      id = 'com.github.etinaa.cucumber-reporting-plugin'
      implementationClass = 'com.github.etinaa.gradle.CucumberReportingPlugin'
    }
  }
}

task packageSources(type: Jar, dependsOn: 'classes') {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives packageSources
}

publishing {
  publications {
    PluginPublication(MavenPublication) {
      from components.java
      artifact packageSources {
        classifier "sources"
      }
      groupId 'com.github.etinaa'
      artifactId 'cucumber-reporting-plugin'
      version project.version
    }
  }
}

bintray {
  user = System.getProperty('BINTRAY_USER') ?: "${BINTRAY_USER}"
  key = System.getProperty('BINTRAY_KEY') ?: "${BINTRAY_KEY}"
  publications = ['PluginPublication']
  pkg {
    repo = 'gradle-plugins'
    name = 'cucumber-reporting-plugin'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/etinaa/cucumber-reporting-plugin.git'
  }
  version {
    name = project.version
    released = new Date()
    attributes = ['gradle-plugin': 'com.github.etinaa.cucumber-reporting-plugin']
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.10.2'
}