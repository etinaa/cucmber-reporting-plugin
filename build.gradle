buildscript {
  repositories {
    jcenter()
  }
}

plugins {
  id 'groovy'
  id 'java-gradle-plugin'
  id 'maven-publish'
  id "com.jfrog.bintray" version "1.8.4"
}

repositories {
  jcenter()
}

sourceSets {
  functionalTest {
    groovy {
      srcDir file('src/functionalTest/groovy')
    }
    resources {
      srcDir file('src/functionalTest/resources')
    }
    compileClasspath += sourceSets.main.output + configurations.testRuntime
    runtimeClasspath += output + compileClasspath
  }
}

group 'com.github.etinaa'
version '0.2.0'

dependencies {
  compile localGroovy()
  compile 'net.masterthought:cucumber-reporting:4.2.2'

  testCompile gradleTestKit()
  testCompile('org.spockframework:spock-core:1.2-groovy-2.4') {
    exclude module: 'groovy-all'
  }
}

gradlePlugin {
  plugins {
    simplePlugin {
      id = 'com.github.etinaa.cucumber-reporting-plugin'
      implementationClass = 'com.github.etinaa.gradle.CucumberReportingPlugin'
    }
  }

  testSourceSets sourceSets.functionalTest
}

task functionalTest(type: Test) {
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

check.dependsOn functionalTest

task packageSources(type: Jar, dependsOn: 'classes') {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives packageSources
}

publishing {
  publications {
    PluginPublication(MavenPublication) {
      from components.java
      artifact packageSources {
        classifier "sources"
      }
      groupId 'com.github.etinaa'
      artifactId 'cucumber-reporting-plugin'
      version project.version
    }
  }
}

bintray {
  user = project.properties['BINTRAY_USER'] ?: System.getProperty('BINTRAY_USER')
  key = project.properties['BINTRAY_KEY'] ?: System.getProperty('BINTRAY_KEY')
  publications = ['PluginPublication']
  pkg {
    repo = 'gradle-plugins'
    name = project.name
    desc = 'Gradle plugin for generating html cucumber reports'
    vcsUrl = 'https://github.com/etinaa/cucumber-reporting-plugin.git'
    licenses = ['MIT']
    labels = ['gradle', 'cucumber', 'reports']
    githubRepo = 'https://github.com/etinaa/cucumber-reporting-plugin'
    version {
      name = project.version
      released = new Date()
      vcsTag = project.version
      attributes = ['gradle-plugin': 'com.github.etinaa.cucumber-reporting-plugin']
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.10.2'
}